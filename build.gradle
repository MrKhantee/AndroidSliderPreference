buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.library'

def eclipseProperties=new Properties()
file('project.properties').withInputStream {
    eclipseProperties.load(it)
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        targetSdkVersion eclipseProperties.getProperty('target')-'android-'

        // note that this code assumes it's building the latest tag, and that all tags are present in the repository;
        // older tags can be built by cloning only a subsection of the history
        versionCode 'git tag'.execute(null, projectDir) .text.readLines().size()
        versionName 'git describe --tags --always HEAD'.execute(null, projectDir).text.trim()
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}
